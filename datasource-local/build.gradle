plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlinx-serialization'
}

android {
    buildToolsVersion rootProject.ext.buildToolsVersion
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        // used by Room, to test migrations
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental":"true",
                        "room.expandProjection":"true"
                ]
            }
        }
    }

    // used by Room, to test migrations
    sourceSets {
        androidTest.assets.srcDirs +=
                files("$projectDir/schemas".toString())
    }

    buildTypes {
        debug {
            minifyEnabled false
            useProguard false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    kapt {
        generateStubs = true
    }

    // https://stackoverflow.com/a/48988779/11419056
    // * Go to Build, Execution, Deployment > Compiler > Kotlin Compiler BUT Other Settings > Kotlin compiler
    // * Change the Target JVM version to 1.8, then Apply
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += [
                "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
                "-Xuse-experimental=kotlinx.coroutines.FlowPreview",
                "-Xuse-experimental=kotlinx.coroutines.ObsoleteCoroutinesApi"
        ]
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':domain')

    // Jetbrains Kotlin Dependencies
    implementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlinx_serialization_version"

    // Dependencies for local unit tests
    testImplementation "junit:junit:$junitVersion"
    testImplementation "androidx.arch.core:core-common:$androidXCoreTesting"
    testImplementation "androidx.arch.core:core-runtime:$androidXCoreTesting"
    testImplementation "androidx.arch.core:core-testing:$androidXCoreTesting"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "io.mockk:mockk:$mockKVersion"

    // Dependency Injection Framework
    // Koin Dependency Injection
    // Koin for Kotlin
    implementation "io.insert-koin:koin-core:$koinVersion"
    // Koin for Android
    implementation "io.insert-koin:koin-android:$koinVersion"
    // Koin Test
    testImplementation("io.insert-koin:koin-test:$koinVersion") {
        exclude group: 'org.mockito'
    }

    // Coroutine
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"

    // LocalDB Frameworks
    // AndroidX RoomDB
    implementation "androidx.room:room-runtime:$androidXRoomVersion"
    kapt "androidx.room:room-compiler:$androidXRoomVersion"
    // optional - Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:$androidXRoomVersion"
    // Test helpers
    testImplementation("androidx.room:room-testing:$androidXRoomVersion")
    // https://youtrack.jetbrains.com/issue/KT-45883
    // - KAPT: "Cannot find setter for field" compiling projects with Room db breaks using 1.5.0-M2
    //   - https://youtrack.jetbrains.com/issue/KT-45883#focus=Comments-27-4825563.0-0
    //      - Simple local workaround is to explicitly add dependency to newer version:
    kapt "org.jetbrains.kotlinx:kotlinx-metadata-jvm:0.2.0"

    // Timber Log
    implementation "com.jakewharton.timber:timber:$timberLogVersion"
}