plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'kotlinx-serialization'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def propFile = rootProject.file("local.properties")
def locProp = new Properties()
locProp.load(propFile.newDataInputStream())

android {
    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "hk.com.lolamove"
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        lolamove {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
        lolamovedev {
            keyAlias keystoreProperties['keyAliasDev']
            keyPassword keystoreProperties['keyPasswordDev']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            minifyEnabled false

            firebaseCrashlytics {
                mappingFileUploadEnabled false
                nativeSymbolUploadEnabled false
            }
        }
        release {
            minifyEnabled true
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            firebaseCrashlytics {
                mappingFileUploadEnabled true
                // Enable processing and uploading of native symbols to Crashlytics servers.
                // By default, this is disabled to improve build speeds.
                // This flag must be enabled to see properly-symbolicated native
                // stack traces in the Crashlytics dashboard.
                nativeSymbolUploadEnabled true
                strippedNativeLibsDir 'build/intermediates/stripped_native_libs/release/out/lib'
                unstrippedNativeLibsDir 'build/intermediates/merged_native_libs/release/out/lib'
            }
        }
    }

    productFlavors {
        flavorDimensions "default"
        prod {
            signingConfig signingConfigs.lolamove

            // TODO:: When multiple environments are setup(i.e. PROD Environment), change this value accordingly
            buildConfigField "String", "REST_API_URL_ENDPOINT", "\"https://mock-api-mobile.dev.lalamove.com/\""
        }
        dev {
            applicationIdSuffix ".dev"
            signingConfig signingConfigs.lolamovedev

            // TODO:: When multiple environments are setup(i.e. DEV Environment), change this value accordingly
            buildConfigField "String", "REST_API_URL_ENDPOINT", "\"https://mock-api-mobile.dev.lalamove.com/\""
        }
        mock {
            applicationIdSuffix ".dev"
            signingConfig signingConfigs.lolamovedev

            // API REST Endpoint pointing to Apiary(https://app.apiary.io/l0lamove)
            buildConfigField "String", "REST_API_URL_ENDPOINT", "\"https://private-cc53a5-l0lamove.apiary-mock.com/\""
        }
    }

    kapt {
        generateStubs = true
    }

    // https://stackoverflow.com/a/48988779/11419056
    // * Go to Build, Execution, Deployment > Compiler > Kotlin Compiler BUT Other Settings > Kotlin compiler
    // * Change the Target JVM version to 1.8, then Apply
    kotlinOptions {
        jvmTarget = "1.8"
        useIR = true

        freeCompilerArgs += [
                "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
                "-Xuse-experimental=kotlinx.coroutines.FlowPreview",
                "-Xuse-experimental=kotlinx.coroutines.ObsoleteCoroutinesApi"
        ]
    }

    buildFeatures {
        // Jetpack Compose
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
        kotlinCompilerVersion kotlin_version
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/main.kotlin_module'
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
            excludes += "**/attach_hotspot_windows.dll"
            excludes += "META-INF/licenses/ASM"
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':domain')
    implementation project(':data')

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Jetbrains Kotlin Dependencies
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlinx_serialization_version"

    // Dependencies for local unit tests
    testImplementation "junit:junit:$junitVersion"
    testImplementation "androidx.arch.core:core-common:$androidXCoreTesting"
    testImplementation "androidx.arch.core:core-runtime:$androidXCoreTesting"
    testImplementation "androidx.arch.core:core-testing:$androidXCoreTesting"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "io.mockk:mockk:$mockKVersion"

    // Jetpack Compose
    implementation "androidx.compose.runtime:runtime:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "androidx.compose.foundation:foundation-layout:$compose_version"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.activity:activity-compose:$androidXActivity"
    implementation "androidx.navigation:navigation-compose:$navigationComposeVersion"
    implementation "com.google.android.material:compose-theme-adapter:$compose_version"

    androidTestImplementation("androidx.compose.ui:ui-test-junit4:$compose_version")
    debugImplementation("androidx.compose.ui:ui-test-manifest:$compose_version")
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"

    // Android Testing Support Library's runner and rules
    androidTestImplementation "androidx.test:runner:$androidXTest"
    androidTestImplementation "androidx.test:rules:$androidXTest"
    androidTestImplementation "org.mockito:mockito-android:$mockitoVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidXTestExt"
    // mockk.io - https://github.com/mockk/mockk/issues/281
    androidTestImplementation("io.mockk:mockk-android:$mockKVersion") {
        exclude module: 'objenesis'
    }

    // AndroidX
    implementation "androidx.core:core:$androidXCoreVersion"
    implementation "androidx.core:core-ktx:$androidXCoreVersion"
    implementation "androidx.annotation:annotation:$androidXAnnotationVersion"
    // AndroidX Activity
    implementation "androidx.activity:activity:$androidXActivity"
    implementation "androidx.activity:activity-ktx:$androidXActivity"
    // AndroidX SplashScreen
    implementation "androidx.core:core-splashscreen:$androidXSplashScreen"

    // Dependency Injection Framework
    // Koin Dependency Injection
    // Koin for Kotlin
    implementation "io.insert-koin:koin-core:$koinVersion"
    // Koin for Android
    implementation "io.insert-koin:koin-android:$koinVersion"
    // Koin AndroidX Scope feature
    implementation "io.insert-koin:koin-androidx-scope:$koinVersion"
    // Koin AndroidX ViewModel feature
    implementation "io.insert-koin:koin-androidx-viewmodel:$koinVersion"
    // Koin Test
    testImplementation("io.insert-koin:koin-test:$koinVersion") {
        exclude group: 'org.mockito'
    }

    // AndroidX Lifecycle
    kapt "androidx.lifecycle:lifecycle-common-java8:$androidXLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidXLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$androidXLifecycleVersion"

    // Coroutine
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"

    // Firebase SDK
    // Import the BoM for the Firebase platform(When using the BoM, you don't specify versions in Firebase library dependencies)
    implementation platform("com.google.firebase:firebase-bom:$firebaseSdkVersion")
    // Declare the dependencies for Authentication
    implementation 'com.google.firebase:firebase-auth-ktx'
    // Declare the dependency for the Firebase Crashlytics NDK library.
    // If you previously declared the Firebase Crashlytics dependency, replace it
    /*implementation "com.google.firebase:firebase-crashlytics-ktx"*/
    implementation 'com.google.firebase:firebase-crashlytics-ndk'

    // Facebook Stetho - A sophisticated debug bridge for Android applications
    implementation "com.facebook.stetho:stetho-okhttp3:$facebookStethoVersion"

    // Timber Log
    implementation "com.jakewharton.timber:timber:$timberLogVersion"

    // debugImplementation because LeakCanary should only run in debug builds.
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"

    // ViewBindingPropertyDelegate
    implementation "com.github.kirich1409:viewbindingpropertydelegate-noreflection:$viewbindingpropertydelegateVersion"

}